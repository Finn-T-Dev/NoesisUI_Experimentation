#include "EffectHelpers.h"

uniforms
{
    fixed _amount;
    fixed _time;
    int _monochromatic;
};

// https://www.shadertoy.com/view/XsX3zB

/* discontinuous pseudorandom uniformly distributed in [-0.5, +0.5]^3 */
float3 random3(float3 c)
{
    float j = 4096.0 * sin(dot(c, float3(17.0, 59.4, 15.0)));
    float3 r;
    r.z = frac(512.0 * j);
    j *= .125;
    r.x = frac(512.0 * j);
    j *= .125;
    r.y = frac(512.0 * j);
    return r - 0.5;
}

/* 3d simplex noise */
float noise(float3 p)
{
    const float F3 = 0.3333333;
    const float G3 = 0.1666667;

    /* 1. find current tetrahedron T and it's four vertices */
    /* s, s + i1, s + i2, s + 1.0 - absolute skewed (integer) coordinates of T vertices */
    /* x, x1, x2, x3 - unskewed coordinates of p relative to each of T vertices*/

    /* calculate s and x */
    float3 s = floor(p + dot(p, float3(F3, F3, F3)));
    float3 x = p - s + dot(s, float3(G3, G3, G3));

    /* calculate i1 and i2 */
    float3 e = step(float3(0.0, 0.0, 0.0), x - x.yzx);
    float3 i1 = e * (1.0 - e.zxy);
    float3 i2 = 1.0 - e.zxy * (1.0 - e);

    /* x1, x2, x3 */
    float3 x1 = x - i1 + G3;
    float3 x2 = x - i2 + 2.0 * G3;
    float3 x3 = x - 1.0 + 3.0 * G3;

    /* 2. find four surflets and store them in d */
    float4 w, d;

    /* calculate surflet weights */
    w.x = dot(x, x);
    w.y = dot(x1, x1);
    w.z = dot(x2, x2);
    w.w = dot(x3, x3);

    /* w fades from 0.6 at the center of the surflet to 0.0 at the margin */
    w = max(0.6 - w, 0.0);

    /* calculate surflet components */
    d.x = dot(random3(s), x);
    d.y = dot(random3(s + i1), x1);
    d.z = dot(random3(s + i2), x2);
    d.w = dot(random3(s + 1.0), x3);

    /* multiply d by w^4 */
    w *= w;
    w *= w;
    d *= w;

    /* 3. return the sum of the four surflets */
    return dot(d, float4(52.0, 52.0, 52.0, 52.0));
}

fixed4 main_effect()
{
    fixed4 c = GetInput();
    float2 pos = GetImagePosition();

    // Limit frequency to 0.5 to avoid aliasing
    float fwidth = max(length(ddx(pos)), length(ddy(pos)));
    float width = min(0.5 * fwidth, 0.5);
    float scale = width / fwidth;

    if (_monochromatic == 1)
    {
        float p = _amount * noise(float3(pos * scale, _time));
        return float4(c.r + p * c.a, c.g + p * c.a, c.b + p * c.a, c.a);
    }
    else
    {
        float r = _amount * noise(float3(pos * scale, _time));
        float g = _amount * noise(float3(pos * scale, _time + 14.0));
        float b = _amount * noise(float3(pos * scale, _time - 23.0));

        return float4(c.r + r * c.a, c.g + g * c.a, c.b + b * c.a, c.a);
    }
}